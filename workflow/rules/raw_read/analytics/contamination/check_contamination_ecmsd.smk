
def get_ecmsd_database(wildcards):
    configured_db = config.get("pipeline", {}).get("raw_reads_processing", {}).get("contamination_analysis", {}).get("tools", {}).get("ecmsd", {}).get("settings", {}).get("database")
    if configured_db:
        return configured_db
    else:
        return "resources/ecmsd_database"

rule ecmsd_database_setup:
    output:
        directory("resources/ecmsd_database")
    conda:
        "../../../../envs/ecmsd.yaml"
    shell:
        """
        ECMSD --create-db --db-folder {output}
        """

rule ecmsd_analyze_contamination:
    input:
        fastq = "{species}/processed/reads/reads_quality_filtered/{sample}_quality_filtered.fastq.gz",
        database = get_ecmsd_database
    output:
        summary         = "{species}/results/contamination_analysis/ecmsd/{individual}/{sample}/mapping/{sample}_Mito_summary.txt",
        paf             = "{species}/results/contamination_analysis/ecmsd/{individual}/{sample}/mapping/{sample}_Mito.paf.gz",
        RMUS            = "{species}/results/contamination_analysis/ecmsd/{individual}/{sample}/mapping/{sample}_Mito_summary.RMUS.txt",
        proportions     = "{species}/results/contamination_analysis/ecmsd/{individual}/{sample}/mapping/{sample}_Mito_summary_genus_proportions.txt",
        genus           = "{species}/results/contamination_analysis/ecmsd/{individual}/{sample}/mapping/{sample}_Mito_summary_genus.txt",
        readlength      = "{species}/results/contamination_analysis/ecmsd/{individual}/{sample}/mapping/{sample}_Mito_summary_genus_ReadLengths.png",
        proportions_png = "{species}/results/contamination_analysis/ecmsd/{individual}/{sample}/mapping/{sample}_Mito_summary_genus_Proportions.png",
    params:
        binsize = config.get("pipeline", {}).get("raw_reads_processing", {}).get("contamination_analysis", {}).get("tools", {}).get("ecmsd", {}).get("settings", {}).get("Binsize", 1000),
        rmus_threshold = config.get("pipeline", {}).get("raw_reads_processing", {}).get("contamination_analysis", {}).get("tools", {}).get("ecmsd", {}).get("settings", {}).get("RMUS_threshold", 0.15),
        mapping_quality = config.get("pipeline", {}).get("raw_reads_processing", {}).get("contamination_analysis", {}).get("tools", {}).get("ecmsd", {}).get("settings", {}).get("mapping_quality", 20),
        taxonomic_hierarchy = config.get("pipeline", {}).get("raw_reads_processing", {}).get("contamination_analysis", {}).get("tools", {}).get("ecmsd", {}).get("settings", {}).get("taxonomic_hierarchy", "genus"),
        prefix = "{sample}"
    threads: 15
    conda:
        "../../../../envs/ecmsd.yaml"
    message: "Running ECMSD contamination analysis for {input.fastq}"
    shell:
        """
        outdir=$(dirname $(dirname {output.summary}))
        mkdir -p "$outdir"

        echo "Running ECMSD for sample {wildcards.sample}"
        echo "Input FASTQ: {input.fastq}"
        echo "Output folder: $outdir"

        ECMSD \
            --fwd {input.fastq} \
            --out "$outdir" \
            --threads {threads} \
            --prefix {params.prefix} \
            --binsize {params.binsize} \
            --RMUS-threshold {params.rmus_threshold} \
            --mapping_quality {params.mapping_quality} \
            --taxonomic-hierarchy {params.taxonomic_hierarchy} \
            --db-folder {input.database} \
            --force
        """

rule ecmsd_merge_hits_per_individual:
    input:
        lambda wildcards: expand("{species}/results/contamination_analysis/ecmsd/{individual}/{sample}/mapping/{sample}_Mito_summary_genus_proportions.txt",
            sample=get_samples_for_species_individual(wildcards.species, wildcards.individual),
            species=wildcards.species,
            individual=wildcards.individual
            )
    output:
        "{species}/results/contamination_analysis/ecmsd/{individual}_Mito_summary_genus_hits_combined.tsv"
    script:
        "../../../../scripts/raw_reads/analytics/contamination/check_contamination_ecmsd_script_ecmsd_merge_hits_per_individual.py"

rule ecmsd_analyze_proportions:
    input:
        report = "{species}/results/contamination_analysis/ecmsd/{individual}/{sample}/mapping/{sample}_Mito_summary_genus_proportions.txt",
        count_reads = "{species}/processed/reads/statistics/{sample}_quality_filtered.count"
    output:
        "{species}/results/contamination_analysis/ecmsd/{individual}/{sample}/pipeline/{sample}_ecmsd_proportions.tsv"
    params:
        sample = "{sample}"
    script:
        "../../../../scripts/raw_reads/analytics/contamination/check_contamination_ecmsd_script_ecmsd_analyze_proportions.py"
